import QtQuick

Item {
    id: escapeMenuOverlayRoot
    visible: false
    property int currentIndex: 0

    signal resumeClicked()
    signal restartClicked()
    signal changeGameParametersClicked()
    signal quitClicked()

    required property real uiScale // scale propagated from UiScaler

    // Semi-transparent dark layer
    Rectangle {
        anchors.fill: parent
        color: "black"
        opacity: 0.5
    }

    Column {
        anchors.centerIn: parent
        spacing: 40 * uiScale

        Repeater {
            model: ["RESUME", "RESTART", "CHANGE GAME PARAMETERS", "QUIT"]

            delegate: Rectangle {
                width: 400 * uiScale
                height: 100 * uiScale
                radius: 16 * uiScale

                color: index === escapeMenuOverlayRoot.currentIndex ? "#3A3A3A" : "#2A2A2A"
                border.color: index === escapeMenuOverlayRoot.currentIndex ? "#5BC0EB" : "#00000000"
                border.width: 10 * uiScale

                Text {
                    anchors.centerIn: parent
                    text: modelData
                    color: "white"
                    font.pixelSize: 25 * uiScale
                    font.bold: true
                }

                MouseArea {
                    anchors.fill: parent
                    hoverEnabled: true
                    onEntered: escapeMenuOverlayRoot.currentIndex = index
                    onClicked: {
                        if (index === 0) escapeMenuOverlayRoot.resumeClicked()
                        else if (index === 1) escapeMenuOverlayRoot.restartClicked()
                        else if (index === 2) escapeMenuOverlayRoot.changeGameParametersClicked()
                        else escapeMenuOverlayRoot.quitClicked()
                    }
                }
            }
        }
    }
}
